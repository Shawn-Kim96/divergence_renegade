//@version=5
indicator(title="Double RSI Divergence Indicator", format=format.price, timeframe="", timeframe_gaps=true)
len = input.int(title="RSI Period", minval=1, defval=14)
src = input(title="RSI Source", defval=close)
lbR = input(title="Pivot Lookback Right", defval=5, display = display.data_window)
lbL = input(title="Pivot Lookback Left", defval=5, display = display.data_window)
rangeUpper = input(title="Max of Lookback Range", defval=60, display = display.data_window)
rangeLower = input(title="Min of Lookback Range", defval=5, display = display.data_window)
plotBull = input(title="Plot Bullish", defval=true, display = display.data_window)
plotBear = input(title="Plot Bearish", defval=true, display = display.data_window)
bearColor = color.red
bullColor = color.green
textColor = color.white
noneColor = color.new(color.white, 100)
osc = ta.rsi(src, len)

plot(osc, title="RSI", linewidth=2, color=#2962FF)
hline(50, title="Middle Line", color=#787B86, linestyle=hline.style_dotted)
obLevel = hline(70, title="Overbought", color=#787B86, linestyle=hline.style_dotted)
osLevel = hline(30, title="Oversold", color=#787B86, linestyle=hline.style_dotted)
fill(obLevel, osLevel, title="Background", color=color.rgb(33, 150, 243, 90))

plFound = na(ta.pivotlow(osc, lbL, lbR)) ? false : true
phFound = na(ta.pivothigh(osc, lbL, lbR)) ? false : true
_inRange(cond) =>
	bars = ta.barssince(cond == true)
	rangeLower <= bars and bars <= rangeUpper

//------------------------------------------------------------------------------
// Regular Bullish
// Osc: Higher Low

oscHL = osc[lbR] > ta.valuewhen(plFound, osc[lbR], 1) and _inRange(plFound[1])

// Price: Lower Low

priceLL = low[lbR] < ta.valuewhen(plFound, low[lbR], 1)

// Second Regular Bullish

oscSecHL = ta.valuewhen(plFound, osc[lbR], 1) > ta.valuewhen(plFound, osc[lbR], 2) and _inRange(plFound[2])
priceSecLL = ta.valuewhen(plFound, low[lbR], 1) < ta.valuewhen(plFound, low[lbR], 2)

bullCondAlert = priceLL and oscHL and plFound
doubleBullCondAlert = bullCondAlert and priceSecLL and oscSecHL
bullCond = plotBull and bullCondAlert

plot(
     plFound ? osc[lbR] : na,
     offset=-lbR,
     title="Regular Bullish",
     linewidth=2,
     color=(bullCond ? bullColor : noneColor),
	 display = display.pane
     )

plotshape(
	 doubleBullCondAlert ? osc[lbR] : na,
	 offset=-lbR,
	 title="Double Regular Bullish Label",
	 text=" Double Bull ",
	 style=shape.labelup,
	 location=location.bottom,
	 color=bullColor,
	 textcolor=textColor
	 )

//------------------------------------------------------------------------------
// Regular Bearish
// Osc: Lower High

oscLH = osc[lbR] < ta.valuewhen(phFound, osc[lbR], 1) and _inRange(phFound[1])

// Price: Higher High

priceHH = high[lbR] > ta.valuewhen(phFound, high[lbR], 1)

// Second Regular Bearish

oscSecLH = ta.valuewhen(phFound, osc[lbR], 1) < ta.valuewhen(phFound, osc[lbR], 2) and _inRange(phFound[2])
priceSecHH = ta.valuewhen(phFound, high[lbR], 1) > ta.valuewhen(phFound, high[lbR], 2)

bearCondAlert = priceHH and oscLH and phFound
doubleBearCondAlert = bearCondAlert and priceSecHH and oscSecLH
bearCond = plotBear and bearCondAlert

plot(
	 phFound ? osc[lbR] : na,
	 offset=-lbR,
	 title="Regular Bearish",
	 linewidth=2,
	 color=(bearCond ? bearColor : noneColor),
	 display = display.pane
	 )

plotshape(
	 doubleBearCondAlert ? osc[lbR] : na,
	 offset=-lbR,
	 title="Double Regular Bearish Label",
	 text=" Double Bear ",
	 style=shape.labeldown,
	 location=location.top,
	 color=bearColor,
	 textcolor=textColor
	 )

alertcondition(doubleBullCondAlert, title='Double Regular Bullish Divergence', message="Found a new Double Regular Bullish Divergence, `Pivot Lookback Right` number of bars to the left of the current bar")
alertcondition(doubleBearCondAlert, title='Double Regular Bearish Divergence', message='Found a new Double Regular Bearish Divergence, `Pivot Lookback Right` number of bars to the left of the current bar')
